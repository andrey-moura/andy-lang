// This tool generates releases notes for the uva-lang project.

class Application
{
    function run()
    {
        // // Get commit hash, prefix and message.
        var ret = system("git log --oneline --no-decorate --no-merges > releases/release_notes.txt");

        // Check if the command was successful. If not, return the error code.
        if (ret)
        {
            return ret;
        }

        // Read the content of the file.
        var file_lines = File.read_all_lines("releases/release_notes.txt");

        foreach(var line in file_lines) {
            var first_space = line.find(" ");

            // Remove the commit hash.
            line.erase!(0, first_space + 1);

            var second_space = line.find(" ");

            if(second_space != -1) {
                // Separetes the commit prefix.
                var prefix = line.substr(0, second_space);
                prefix.to_lower_case!

                if(prefix == "fix:" || prefix == "feat:") {
                    var message = line.substr(second_space + 1, -1);
                    message.capitalize!

                    puts(message);
                }
            }
        }
    }
}